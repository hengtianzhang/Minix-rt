# SPDX-License-Identifier: Apache-2.0

#.rst:
# version.cmake
# -------------
#
# Inputs:
#
#   ``*VERSION*`` and other constants set by
#   maintainers in ``${MINIX_RT_BASE}/VERSION``
#
# Outputs with examples::
#
#   MINIX_RT_PROJECT_VERSION           1.14.99.07
#   MINIX_RT_VERSION_STRING    "1.14.99-extraver"
#
#   MINIX_RT_VERSION_MAJOR       1
#   MINIX_RT_VERSION_MINOR        14
#   MINIX_RT_PATCHLEVEL             99
#   MINIXRTVERSION            0x10E6307
#   MINIX_RT_VERSION_NUMBER    0x10E63
#   MINIX_RT_VERSION_CODE        69219
#
# Most outputs are converted to C macros, see ``version.h.in``
#
# See also: independent and more dynamic ``BUILD_VERSION`` in
# ``git.cmake``.


include(${MINIX_RT_BASE}/cmake/hex.cmake)
file(READ ${MINIX_RT_BASE}/VERSION ver)

string(REGEX MATCH "VERSION_MAJOR = ([0-9]*)" _ ${ver})
set(PROJECT_VERSION_MAJOR ${CMAKE_MATCH_1})

string(REGEX MATCH "VERSION_MINOR = ([0-9]*)" _ ${ver})
set(PROJECT_VERSION_MINOR ${CMAKE_MATCH_1})

string(REGEX MATCH "PATCHLEVEL = ([0-9]*)" _ ${ver})
set(PROJECT_VERSION_PATCH ${CMAKE_MATCH_1})

string(REGEX MATCH "VERSION_TWEAK = ([0-9]*)" _ ${ver})
set(PROJECT_VERSION_TWEAK ${CMAKE_MATCH_1})

string(REGEX MATCH "EXTRAVERSION = ([a-z0-9]*)" _ ${ver})
set(PROJECT_VERSION_EXTRA ${CMAKE_MATCH_1})

# Temporary convenience variable
set(PROJECT_VERSION_WITHOUT_TWEAK ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH})

if(PROJECT_VERSION_EXTRA)
  set(PROJECT_VERSION_EXTRA_STR "-${PROJECT_VERSION_EXTRA}")
endif()

if(PROJECT_VERSION_TWEAK)
  set(MINIX_RT_PROJECT_VERSION ${PROJECT_VERSION_WITHOUT_TWEAK}.${PROJECT_VERSION_TWEAK})
else()
  set(MINIX_RT_PROJECT_VERSION ${PROJECT_VERSION_WITHOUT_TWEAK})
endif()

set(PROJECT_VERSION_STR ${MINIX_RT_PROJECT_VERSION}${PROJECT_VERSION_EXTRA_STR})

if(DEFINED BUILD_VERSION)
  set(BUILD_VERSION_STR ", build: ${BUILD_VERSION}")
endif()

if (NOT NO_PRINT_VERSION)
  message(STATUS "Minix-rt version: ${PROJECT_VERSION_STR} (${MINIX_RT_BASE})${BUILD_VERSION_STR}")
endif()

set(MAJOR ${PROJECT_VERSION_MAJOR}) # Temporary convenience variable
set(MINOR ${PROJECT_VERSION_MINOR}) # Temporary convenience variable
set(PATCH ${PROJECT_VERSION_PATCH}) # Temporary convenience variable

math(EXPR KERNEL_VERSION_NUMBER_INT "(${MAJOR} << 16) + (${MINOR} << 8)  + (${PATCH})")
math(EXPR KERNELVERSION_INT         "(${MAJOR} << 24) + (${MINOR} << 16) + (${PATCH} << 8) + (${PROJECT_VERSION_TWEAK})")

to_hex(${KERNEL_VERSION_NUMBER_INT} MINIX_RT_VERSION_NUMBER)
to_hex(${KERNELVERSION_INT}         MINIXRTVERSION)

set(MINIX_RT_VERSION_MAJOR      ${PROJECT_VERSION_MAJOR})
set(MINIX_RT_VERSION_MINOR      ${PROJECT_VERSION_MINOR})
set(MINIX_RT_PATCHLEVEL         ${PROJECT_VERSION_PATCH})

if(PROJECT_VERSION_EXTRA)
  set(MINIX_RT_VERSION_STRING     "\"${PROJECT_VERSION_WITHOUT_TWEAK}-${PROJECT_VERSION_EXTRA}\"")
else()
  set(MINIX_RT_VERSION_STRING     "\"${PROJECT_VERSION_WITHOUT_TWEAK}\"")
endif()

set(MINIX_RT_VERSION_CODE ${KERNEL_VERSION_NUMBER_INT})

# Cleanup convenience variables
unset(MAJOR)
unset(MINOR)
unset(PATCH)
unset(PROJECT_VERSION_WITHOUT_TWEAK)
